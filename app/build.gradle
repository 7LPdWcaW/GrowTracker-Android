apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: "androidx.navigation.safeargs"

repositories {
	mavenCentral()
}

def travis = System.getenv("CI") == "true"

def getCommitCountTotal = { ->
	try
	{
		def commitCount = "git rev-list HEAD --count".execute().text.trim()
		return (commitCount == "") ? 0 : commitCount.toInteger()
	}
	catch (e)
	{
		return 1
	}
}

android {
	compileOptions {
		sourceCompatibility = 1.8
		targetCompatibility = 1.8
	}

	kotlinOptions {
		jvmTarget = "1.8"
	}

	compileSdkVersion 29
	buildToolsVersion "29.0.2"

	defaultConfig {
		applicationId "me.anon.grow"
		minSdkVersion 17
		targetSdkVersion 28
		versionCode 1370 //getCommitCountTotal()
		versionName "2.6.1"
		versionNameSuffix(travis ? "-alpha" : "")
		multiDexEnabled true

		resValue "string", "version_date", System.currentTimeMillis().toString()

		javaCompileOptions {
			annotationProcessorOptions {
				includeCompileClasspath false
			}
		}

		vectorDrawables.useSupportLibrary = true
	}

	lintOptions {
		abortOnError false
	}

	flavorDimensions "default"

	productFlavors {
		production {
			dimension "default"
			buildConfigField "Boolean", "DISCRETE", "false"

			manifestPlaceholders = [
					"appType": "original"
			]

			resValue "string", "app_name", "GrowTracker"
		}

		en {
			resConfig "en"
			buildConfigField "Boolean", "DISCRETE", "false"

			manifestPlaceholders = [
					"appType": "en"
			]

			resValue "string", "app_name", "GrowTracker"
		}

		discrete {
			dimension "default"
			buildConfigField "Boolean", "DISCRETE", "true"

			manifestPlaceholders = [
					"appType": "discrete"
			]

			resValue "string", "app_name", "Tracker"
		}
	}

	signingConfigs {
		release {
			// configure keystore
			storeFile = file("../keystore.jks")
			storePassword = System.getenv("keystore_password")
			keyAlias = System.getenv("keystore_alias")
			keyPassword = System.getenv("keystore_alias_password")
			v2SigningEnabled = true
			v1SigningEnabled = true
		}
	}

	buildTypes {
		debug {
			resValue "string", "app_name", "Dev GrowTracker"
			applicationIdSuffix = ".debug"
		}

		release {
			signingConfig signingConfigs.release
		}
	}
}

dependencies {
	implementation 'androidx.multidex:multidex:2.0.1'
	implementation 'androidx.core:core:1.2.0'
	implementation 'androidx.appcompat:appcompat:1.1.0'
	implementation 'androidx.preference:preference:1.1.0'
	implementation 'androidx.recyclerview:recyclerview:1.1.0'
	implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
	implementation 'androidx.cardview:cardview:1.0.0'
	implementation 'androidx.exifinterface:exifinterface:1.2.0'
	implementation 'androidx.documentfile:documentfile:1.0.1'

	implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"
	implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0"
	implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.2.0"
	implementation "androidx.fragment:fragment:1.2.4"
	implementation "androidx.fragment:fragment-ktx:1.2.4"
	def nav_version = "2.3.0-alpha06"

	// Kotlin
	implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
	implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

	implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.0"

	implementation 'com.github.prolificinteractive:material-calendarview:2.0.0'
	implementation 'com.jakewharton.threetenabp:threetenabp:1.1.1'

	implementation 'com.google.android.material:material:1.2.0-alpha05'
	implementation 'com.google.android:flexbox:1.1.0'
	implementation 'com.esotericsoftware:kryo:3.0.3'
	implementation 'com.squareup.moshi:moshi-kotlin:1.8.0'
	implementation 'com.squareup:otto:1.3.8'
	implementation 'com.github.PhilJay:MPAndroidChart:v2.1.6'
	implementation 'com.nostra13.universalimageloader:universal-image-loader:1.9.5'
	implementation 'com.davemorrissey.labs:subsampling-scale-image-view:3.6.0'
	implementation 'net.lingala.zip4j:zip4j:1.3.2'

	api "org.jetbrains.kotlin:kotlin-reflect:1.3.61"
	api "org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.3.61"

	kapt 'com.squareup.moshi:moshi-kotlin-codegen:1.8.0'
}

androidExtensions {
	experimental = true
}
